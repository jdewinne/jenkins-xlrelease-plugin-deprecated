group = "com.xebialabs.xlrelease.plugins.ci.jenkins"
version = "2.2.1-SNAPSHOT"
description = "Invoke Jenkins from XL Release"

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'java'

repositories {
  mavenCentral()
  maven {
    url "http://repo.jenkins-ci.org/public/"
  }
}

configurations {
  compile.transitive = true
  // Engine packager requires JDOM2. Exclude from config so that it is included in 
  // the hpi Web-Inf lib.
  providedRuntime.exclude group: 'jdom', module: 'jdom' 
  // needs Java 7, borks on Java 6 setups
  providedRuntime.exclude group: 'de.schlichtherle.truezip', module: 'truezip-path'
  all*.exclude group: 'com.thoughtworks.xstream', module: "xstream"
  // this unused artifact lives in a netbeans repository which is flakey, so exclude it.
  testCompile.exclude group: 'org.netbeans.modules', module: "org-netbeans-insane"
}

// This is the latest version not to bork on a Java 6 Jenkins master/slave-setup.
// Later versions reference NIO.2, resulting in a NoClassDefFoundError on java.nio.file.Path
def compatibleTruezipVersion = "7.1.4"

configurations.all {
  resolutionStrategy {
    force "de.schlichtherle.truezip:truezip-driver-file:$compatibleTruezipVersion"
    force "de.schlichtherle.truezip:truezip-driver-zip:$compatibleTruezipVersion"
    force "de.schlichtherle.truezip:truezip-file:$compatibleTruezipVersion"
    force "de.schlichtherle.truezip:truezip-kernel:$compatibleTruezipVersion"
    force "de.schlichtherle.truezip:truezip-swing:$compatibleTruezipVersion"
  }
}

dependencies {
  compile "org.jenkins-ci.main:cli:1.509.1"
  compile "org.jenkins-ci.main:jenkins-core:1.509.1"
  compile "org.slf4j:slf4j-api:1.7.5"

  compile 'com.google.code.findbugs:jsr305:2.0.2'
  compile 'findbugs:annotations:1.0.0'

  testCompile "junit:junit:4.11"
  testCompile "org.mockito:mockito-all:1.9.5"
}